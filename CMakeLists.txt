cmake_minimum_required(VERSION 3.10)
string(REGEX MATCH [[[0-9](\.[0-9])*]] VERSION "$ENV{GITHUB_REF_NAME}")
if(NOT VERSION)
  set(VERSION 0.0.0.0)
endif()
set(HOMEPAGE_URL https://github.com/Freed-Wu/undollar)
project(
  undollar
  VERSION ${VERSION}
  DESCRIPTION "strip the dollar sign from the beginning of the terminal command"
  HOMEPAGE_URL ${HOMEPAGE_URL}
  LANGUAGES)

include(GNUInstallDirs)
install(
  FILES bin/$
  PERMISSIONS
    OWNER_READ
    OWNER_WRITE
    OWNER_EXECUTE
    GROUP_READ
    GROUP_EXECUTE
    WORLD_READ
    WORLD_EXECUTE
  DESTINATION ${CMAKE_INSTALL_BINDIR})
install(
  FILES bin/$
  PERMISSIONS
    OWNER_READ
    OWNER_WRITE
    OWNER_EXECUTE
    GROUP_READ
    GROUP_EXECUTE
    WORLD_READ
    WORLD_EXECUTE
  DESTINATION ${CMAKE_INSTALL_BINDIR}
  RENAME %)

option(ENABLE_BASH_COMPLETION "install bash completion" ON)
option(ENABLE_ZSH_COMPLETION "install zsh completion" ON)
set(BASH_COMPLETION_COMPLETIONSDIR
    ""
    CACHE STRING "directory for bash completion")
set(ZSH_COMPLETION_COMPLETIONSDIR
    ""
    CACHE STRING "directory for zsh completion")

if(ENABLE_BASH_COMPLETION)
  if(NOT BASH_COMPLETION_COMPLETIONSDIR)
    find_package(bash-completion)
  endif()
  if(NOT BASH_COMPLETION_COMPLETIONSDIR)
    set(BASH_COMPLETION_COMPLETIONSDIR
        "${CMAKE_INSTALL_DATAROOTDIR}/bash-completion/completions")
  endif()
  install(FILES completions/bash/$
          DESTINATION ${BASH_COMPLETION_COMPLETIONSDIR})
endif()

if(ENABLE_ZSH_COMPLETION)
  if(NOT ZSH_COMPLETION_COMPLETIONSDIR)
    set(ZSH_COMPLETION_COMPLETIONSDIR
        "${CMAKE_INSTALL_DATAROOTDIR}/zsh/site-functions")
  endif()
  install(FILES completions/zsh/_$ DESTINATION ${ZSH_COMPLETION_COMPLETIONSDIR})
endif()

set(CPACK_PACKAGE_CONTACT ${HOMEPAGE_URL}/issues)
set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Wu Zhenyu <wuzhenyu@ustc.edu>")
set(CPACK_RPM_PACKAGE_LICENSE GPL3)
set(CPACK_RPM_PACKAGE_URL ${HOMEPAGE_URL})
include(CPack)
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
set(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
set(CPACK_ARCHIVE_THREADS 0)
set(CPACK_THREADS 0)
